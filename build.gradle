plugins {
    id 'java'
    id 'org.springframework.boot' version '2.1.4.RELEASE'
    id "org.sonarqube" version "2.7"
    id 'jacoco'
    id 'io.franzbecker.gradle-lombok' version '3.0.0'
}

group 'com.algalopez.ranking'
version project_version

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {

    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: spring_boot_version
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc', version: spring_boot_version
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: spring_boot_version
    compile group: 'org.springframework.security.oauth.boot', name: 'spring-security-oauth2-autoconfigure', version: spring_boot_version
    compile group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: mariadb_version

    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: spring_boot_version
    testCompile group: 'org.springframework.security', name: 'spring-security-test', version: '5.1.5.RELEASE'
}

sonarqube {
    properties {
        property 'sonar.projectKey', 'Ranking'
        property 'sonar.projectName', 'Ranking'
        property 'sonar.projectVersion', project_version
        property 'sonar.language', 'java'
        property 'sonar.sources', 'src/main/java'
        property 'sonar.tests', 'src/test/java'
        property 'sonar.sourceEncoding', 'UTF-8'
        property 'sonar.exclusions', '**/RankingApplication.java, **/*Config.java, **/*Constants.java'
        property 'sonar.coverage.exclusions', '**/RankingApplication.java, **/*Config.java, **/*Constants.java'
    }
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
    }
}

task createDatabase(type: Exec) {
    group = 'Ranking'
    workingDir "${rootDir}/database"
    commandLine 'sh', './PrepareDatabase.sh'
}

task populateDatabase(type: Exec) {
    group = 'Ranking'
    workingDir "${rootDir}/database"
    commandLine 'sh', './PopulateDatabase.sh'
}

task copyArtifacts(type: Copy) {
    group 'Ranking'
    from 'build/libs/'
    include 'Ranking-*.jar'
    into "deliver/"
}

task zipArtifacts(type: Zip, dependsOn: ['copyArtifacts']) {
    group 'Ranking'
    archiveName = "Ranking.zip"

    from "deliver/"
    exclude '*.zip'
    exclude '.gitkeep'
    destinationDir = file('deliver')
}
