plugins {
    id 'java'
    id 'org.springframework.boot' version '2.2.0.RELEASE'
    id "org.sonarqube" version "2.8"
    id 'jacoco'
    id 'io.franzbecker.gradle-lombok' version '3.0.0'
}

group 'com.algalopez.ranking'
version project_version

sourceCompatibility = 11

repositories {
    mavenCentral()
}

wrapper {
    gradleVersion = '5.6.3'
}

dependencies {

    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: spring_boot_version
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc', version: spring_boot_version
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: spring_boot_version
    implementation group: 'org.springframework.security.oauth.boot', name: 'spring-security-oauth2-autoconfigure', version: spring_boot_version
    implementation group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: mariadb_version
    implementation group: 'io.springfox', name: 'springfox-swagger2', version: swagger_version
    implementation group: 'io.springfox', name: 'springfox-swagger-ui', version: swagger_version

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: spring_boot_version
    testImplementation group: 'org.springframework.security', name: 'spring-security-test', version: spring_security_version
}

sonarqube {
    properties {
        property 'sonar.projectKey', 'Ranking'
        property 'sonar.projectName', 'Ranking'
        property 'sonar.projectVersion', version
        property 'sonar.language', 'java'
        property 'sonar.sources', 'src/main/java'
        property 'sonar.tests', 'src/test/java'
        property 'sonar.sourceEncoding', 'UTF-8'
        property 'sonar.exclusions', '**/RankingApplication.java, **/*Config.java, **/*Constants.java'
        property 'sonar.coverage.exclusions', '**/RankingApplication.java, **/*Config.java, **/*Constants.java'
    }
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
    }
}

task createDatabase(type: Exec) {
    group = 'Ranking'
    workingDir "${rootDir}/database"
    commandLine 'sh', './PrepareDatabase.sh'
}

task populateDatabase(type: Exec) {
    group = 'Ranking'
    workingDir "${rootDir}/database"
    commandLine 'sh', './PopulateDatabase.sh'
}

task copyArtifacts(type: Copy) {
    group 'Ranking'
    from 'build/libs/'
    include 'Ranking-*.jar'
    into "artifact/"
}

task zipArtifacts(type: Zip, dependsOn: ['copyArtifacts']) {
    group 'Ranking'

    from "artifact/"
    exclude '*.zip'
    exclude '.gitkeep'
    destinationDirectory = file('artifact')
}
